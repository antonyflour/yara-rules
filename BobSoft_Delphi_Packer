import "pe"

rule BobSoft_Delphi_Packer {
meta:
	description = "Yara Rule for BobSoft Delphi Packer"
	author = "Cybaze - Yoroi ZLab"
	last_updated = "2019-07-10"
	tlp = "white"
	category = "informational"
strings:
	$h1 = {4D 5A 50}
	$bob1 = {55 8B EC 83 C4 F0 B8 ?? ?? ?? ?? E8}
	$bob2 = {55 8B EC 83 C4 F0 53 56 B8 ?? ?? ?? ?? E8 ?? ?? ?? ?? 33 C0 55 68 ?? ?? ?? ?? 64 FF 30 64 89 20 B8}
	$bob3 = {55 8B EC 83 C4 F0 53 B8 ?? ?? ?? ?? E8 ?? ?? ?? ?? 33 C0 55 68 ?? ?? ?? ?? 64 FF 30 64 89 20 B8 ?? ?? ?? ?? E8}
	$s1 = "SOFTWARE\\Borland\\Delphi\\RTL" ascii wide
	$s2 = "LoadResource"
	$s3 = "FindResource"
condition:
	$h1 and all of ($s*) and 1 of ($bob*)
}
